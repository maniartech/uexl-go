{
package parser

import (
    "fmt"

    "github.com/maniartech/uexl_go/ast"
)

// Ref: Operator Precedence in JS
//      https://www.w3schools.com/js/js_precedence.asp
//      https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Operators/Operator_precedence
}

/////////////////// Main Input //////////////////////

Input <- expr:Expr rest:PipedExpr* EOF {
  // Input
  return parseInput(string(c.text), expr, rest, c.pos.offset, c.pos.line, c.pos.col)
}

///////////////////// Expressions /////////////////////

Expr <- _ first:Bit rest:( _ LogOp _ Bit )* _ {
  // Expr:
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

LogOp <- ( "&&" / "||" ) {
  return string(c.text), nil
}

Bit <- first:Equility rest:( _ BitOp _ Equility )* {
  // fmt.Println("Bit:", first, rest)
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

BitOp <- ( '&' / '|' / '^' ) {
  return string(c.text), nil
}

Equility <- first:Comp rest:( _ EqOp _ Comp )* {
  // Comp:
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

EqOp <- ( "==" / "!=" ) {
  return string(c.text), nil
}


Comp <- first:Shift rest:( _ CmpOp _ Shift )* {
  // Comp:
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

CmpOp <- ( "<=" / ">=" / '<' / '>' ) {
  return string(c.text), nil
}

Shift <- first:Sum rest: ( _ ShiftOp _ Sum )* {
  // fmt.Println("Shift:", first, rest)
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

ShiftOp <- ( "<<" / ">>") {
  return string(c.text), nil
}

Sum <- first:Mul rest:( _ AddOp _ Mul )* {
  // fmt.Println("Sum:", first, rest)
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

AddOp <- ( '+' / '-' ) {
  return string(c.text), nil
}

Mul <- first:Mod rest:( _ MulOp _ Mod )* {
  // fmt.Println("Mul:", first, rest)
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

MulOp <- ( '*' / '/' ) {
  return string(c.text), nil
}

Mod <- first:DotExpr rest:( _ ModOp _ DotExpr )* {
  // fmt.Println("Mod:", first, rest)
  return parseExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

ModOp <- ( "%" ) {
  return string(c.text), nil
}


///////////////////// Dot Expression /////////////////////

DotExpr <- _ first:Factor rest:( _ DotOp _ Identifier )* _ {
  // Expr:
  return parseDotExpression(string(c.text), first, rest, c.pos.offset, c.pos.line, c.pos.col)
}

DotOp <- '.' {
  return string(c.text), nil
}

///////////////////// Factor /////////////////////

Factor <- '(' expr:Expr ')' {
  return expr, nil
} / value:Value {
  return value, nil
}

/////////////////////// Piped Expressions ////////////////////////

PipedExpr <- "|" pipeType:PipeType? ":" expr:Expr {
  return parsePipe(pipeType, expr), nil
}

PipeType <- [a-z]+ {
  return string(c.text), nil
}

//////////////// Operators Definitions //////////////////////


///////////////////// Values /////////////////////

Value <- val:( Number / Bool / Null / String / Array / Object / Function / Identifier ) {
    return val, nil
}

///////////////////// Number /////////////////////

Number <- Integer ( '.' DecimalDigit+ )? Exponent? {
  return ast.NewNumberNode(
    string(c.text), c.pos.offset, c.pos.line, c.pos.col,
  )
}

Integer <- [+-]? DecimalDigit+ {
  return strconv.Atoi(string(c.text))
}

Exponent <- 'e'i [+-]? DecimalDigit+

DecimalDigit <- [0-9]

HexDigit <- [0-9a-f]i

///////////////////// Boolean /////////////////////

Bool <- ( "true" / "false") {
  return ast.NewBooleanNode(string(c.text), c.pos.offset, c.pos.line, c.pos.col)
}

///////////////////// Null /////////////////////

Null <- ( "null" ) {
  return ast.NewNullNode(string(c.text), c.pos.offset, c.pos.line, c.pos.col), nil
}

///////////////////// String /////////////////////

String <- (DoubleQuoteString / SingleQuoteString / RawStringSingleQuote / RawStringDoubleQuote)

DoubleQuoteString â† '"' ( !EscapedChar . / '\\' EscapeSequence )* '"' {
  return ast.NewStringNode(c.text, c.pos.offset, c.pos.line, c.pos.col)
}

SingleQuoteString <- '\'' ( !EscapedChar . / '\\' EscapeSequence )* '\'' {
  return ast.NewStringNode(c.text, c.pos.offset, c.pos.line, c.pos.col)
}

// Raw string literals starts with r and quotes (double or single) and
// ends with the same quote. Raw string literals do not process escape
// sequences and ignore line breaks. However, a quote can be escaped by
// using two consecutive quotes.
RawStringSingleQuote <- 'r' '\'' ( !'\'' . / '\'' '\'' )* '\'' {
  return ast.NewStringNode(c.text, c.pos.offset, c.pos.line, c.pos.col)
}

RawStringDoubleQuote <- 'r' '"' ( !'"' . / '"' '"' )* '"' {
  return ast.NewStringNode(c.text, c.pos.offset, c.pos.line, c.pos.col)
}

EscapedChar <- [\x00-\x1f"'\\]

EscapeSequence <- SingleCharEscape / UnicodeEscape

SingleCharEscape <- ["\\/bfnrt]

UnicodeEscape <- 'u' HexDigit HexDigit HexDigit HexDigit

////////////////////// Array //////////////////////

Array <- '[' _ vals:( Expr ( ',' _ Expr )* )? ']' {
    return parseArray(vals, c.text, c.pos.offset, c.pos.line, c.pos.col)
}

Object <- '{' _ vals:( String _ ':' _ Expr ( ',' _ String _ ':' _ Expr )* )? '}' {
    return parseObject(vals, c.text, c.pos.offset, c.pos.line, c.pos.col)
}

////////////////////// Identifier //////////////////////

// Identifier <- IdSet ('.'  IdSet)* {
Identifier <- IdSet* {
  return ast.NewIdentifierNode(string(c.text), c.pos.offset, c.pos.line, c.pos.col)
}

IdSet <- [a-zA-Z0-9$_@]+ {
  return string(c.text), nil
}

////////////////////// Function //////////////////////

Function <- fn:IdSet+ _ '(' _ params:( Expr ( ',' _ Expr )* )? ')' {
  return parseFunction(string(c.text), fn, params,  c.pos.offset, c.pos.line, c.pos.col)
}

////////////////////// Whitespace //////////////////////

_ "whitespace" <- [ \n\t\r]*

////////////////////// EOF //////////////////////

EOF <- !.
